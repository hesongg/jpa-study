### 실전! Querydsl

#### Reference) 
	* 실전! Querydsl (인프런)

#### 작성 코드
- https://github.com/hesongg/Querydsl-Practice

<br>

### 프로젝트 환경 설정

<br>

#### Querydsl 설정

- 스프링 부트 2.6 이상 사용 시 다음 내용 참고
	- ```build.gradle```
		```gradle
		buildscript {
			ext {
				queryDslVersion = "5.0.0"
			}
		}

		plugins {
			id 'org.springframework.boot' version '2.7.4'
			id 'io.spring.dependency-management' version '1.0.14.RELEASE'

			//querydsl 추가
			id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"

			id 'java'
		}

		group = 'study'
		version = '0.0.1-SNAPSHOT'
		sourceCompatibility = '11'

		configurations {
			compileOnly {
				extendsFrom annotationProcessor
			}
		}

		repositories {
			mavenCentral()
		}

		dependencies {
			implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
			implementation 'org.springframework.boot:spring-boot-starter-web'

			//querydsl 추가
			implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
			annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}"

			compileOnly 'org.projectlombok:lombok'
			runtimeOnly 'com.h2database:h2'
			annotationProcessor 'org.projectlombok:lombok'
			testImplementation 'org.springframework.boot:spring-boot-starter-test'

			//테스트에서 lombok 사용
			testCompileOnly 'org.projectlombok:lombok'
			testAnnotationProcessor 'org.projectlombok:lombok'
		}

		tasks.named('test') {
			useJUnitPlatform()
		}

		//querydsl 추가 시작
		def querydslDir = "$buildDir/generated/querydsl"
		querydsl {
			jpa = true
			querydslSourcesDir = querydslDir
		}
		sourceSets {
			main.java.srcDir querydslDir
		}
		configurations {
			querydsl.extendsFrom compileClasspath
		}
		compileQuerydsl {
			options.annotationProcessorPath = configurations.querydsl
		}
		//querydsl 추가 끝
		```
	
<br>

- 검증용 Q 타입 생성 (IntelliJ)
	- Gradle -> Tasks -> build -> clean
	- Gradle -> Tasks -> other -> compileQuerydsl

- Gradle 콘솔 사용법
	- ```./gradlew clean compileQuerydsl```

- Q 타입 생성 확인
	- build -> generated -> querydsl
		- ```study.querydsl.entity.QHello.java``` 파일이 생성되어 있어야 함
	
<br>

- 참고
	- Q타입은 컴파일 시점에 자동 생성되므로 버전관리(GIT)에 포함하지 않는 것이 좋다. 
	- 앞서 설정에서 생성 위치를 gradle build 폴더 아래 생성되도록 했기 때문에 이 부분도 자연스럽게 해결된다. 
		- (대부분 gradle build 폴더를 git에 포함하지 않는다.)

<br>

#### 스프링 부트 설정 - JPA, DB

- ```application.yml```
	```yml
	spring:
	  datasource:
		url: jdbc:h2:tcp://localhost/~/querydsl
		username: sa
		password:
		driver-class-name: org.h2.Driver

	  jpa:
		hibernate:
		  ddl-auto: create
		properties:
		  hibernate:
			# show_sql: true
			format_sql: true

	logging.level:
	  org.hibernate.SQL: debug
	  org.hibernate.type: trace
	```
	
	- ```spring.jpa.hibernate.ddl-auto: create```
		- 이 옵션은 애플리케이션 실행 시점에 테이블을 drop 하고, 다시 생성한다
	
	- 참고: 모든 로그 출력은 가급적 로거를 통해 남겨야 한다.
		- ```show_sql``` : 옵션은 ```System.out``` 에 하이버네이트 실행 SQL을 남긴다.
		- ```org.hibernate.SQL``` : 옵션은 logger를 통해 하이버네이트 실행 SQL을 남긴다

<br>

- 쿼리 파라미터 로그 남기기
	- 로그에 다음을 추가하기 ```org.hibernate.type``` : SQL 실행 파라미터를 로그로 남긴다.
	- 외부 라이브러리 사용
		- https://github.com/gavlyukovskiy/spring-boot-data-source-decorator
			- 스프링 부트를 사용하면 이 라이브러리만 추가하면 된다.
				- ```implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8'```

		- 쿼리 파라미터를 로그로 남기는 외부 라이브러리는 시스템 자원을 사용하므로, 개발 단계에서는 편하게 사용해도 된다. 
			- 하지만 운영시스템에 적용하려면 꼭 성능테스트를 하고 사용하는 것이 좋다.

<br>
